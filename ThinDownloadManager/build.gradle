apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = "1.3.0"
group = "com.mani"

def siteUrl = 'https://github.com/smanikandan14/ThinDownloadManager.git'
def gitUrl = 'https://github.com/smanikandan14/ThinDownloadManager.git'

android {
    compileSdkVersion 23
    buildToolsVersion "21.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "1"
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "thindownloadmanager"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

task deleteJar(type: Delete) {
    delete 'libs/thindldman.jar'
}

task createJar(type: Copy) {
    from('build/intermediates/bundles/debug/')
    into('libs/')
    include('classes.jar')
    rename('classes.jar', 'thindldman.jar')
}

createJar.dependsOn(deleteJar, assembleDebug)

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'To download files and to avoid using DOWNLOAD_WITHOUT_NOTIFICATION permission if you are using Android\'s DownloadManager in your apps'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'smanikandan14'
                        name 'Mani Selvaraju'
                        email 'smanikandan14@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

apply from: '../maven_push.gradle'
